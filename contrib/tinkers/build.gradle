buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

apply plugin: 'forge'

version = "0.1"
group= "net.asrex"
archivesBaseName = "skillful-tinkers"

repositories {
    mavenLocal()
    mavenCentral()
    
    //
    // TiC repos
    //
    
    // dependencies
    maven {
        name "CB Maven FS"
        url "http://chickenbones.net/maven/"
    }
    maven {
        name "ProfMobius Maven FS"
        url "http://mobiusstrip.eu/maven"
    }
    
    // main TiC build server
    maven {
        name = "progwml6"
        url = "http://dvs1.progwml6.com/files/maven/"
    }
}

minecraft {
    version = "1.7.10-10.13.2.1230"
    runDir = "minecraft"
}

configurations {
    provided
}

sourceSets {
    main {
        compileClasspath += configurations.provided 
    }
}

dependencies {
    compile 'net.asrex:skillful:0.1:deobf'
    compile 'org.yaml:snakeyaml:1.14'
    compile 'org.projectlombok:lombok:1.14.8'
    
    // build version info gratuitously stolen from iguanatweaks as it doesn't
    // seem to be published elsewhere
    compile 'tconstruct:TConstruct:1.7.10-1.8.2.build880:deobf'
}

processResources {
    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

targetCompatibility = 7
sourceCompatibility = 7

task javadocs(type: Javadoc) {
  source = sourceSets.main.allJava
  failOnError = false
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'src'
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
}

task javadocJar(type: Jar, dependsOn: javadocs) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

runClient {
    jvmArgs "-Xmx4096m"
}

build.dependsOn sourceJar, deobfJar